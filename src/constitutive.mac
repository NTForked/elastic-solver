load(vect)$
load(eigen)$
load(hj_fortran2)$

optimprefix : tt$

f : openw("constitutive.f90")$

block(
  [X, D, volume, lam, miu, lam1, miu1, c],
  X : genmatrix(X, 3, 4),
  D : genmatrix(D, 3, 3),
  volume : genmatrix(volume, 1, 1),
  lam : genmatrix(lam, 1, 1),
  miu : genmatrix(miu, 1, 1),
  lam1 : genmatrix(lam1, 1, 1),
  miu1 : genmatrix(miu1, 1, 1),
  c : genmatrix(c, 1, 1),
  
  /* stvk elastic constitutive model */
  stvk_tet(X, D, volume, lam, miu) := block(
  [DS, F, E],
  DS : addcol(col(X, 2) - col(X, 1), 
	      col(X, 3) - col(X, 1),
	      col(X, 4) - col(X, 1)),
  F : DS . D,
  E : 0.5 * (transpose(F) . F - ident(3)),
  volume * ( miu * mat_norm(E, frobenius)^2 + 0.5 * lam * mat_trace(E)^2 )
  ),  
  with_stdout(f, val_jac_hes_to_f90(stvk_tet, [X, D, volume, lam, miu])),
  
  /* linear elastic constitutive model */
  linear_elastic_tet(X, D, volume, lam, miu) := block(
  [DS, F, e],
  DS : addcol(col(X, 2) - col(X, 1), 
	      col(X, 3) - col(X, 1),
	      col(X, 4) - col(X, 1)),
  F : DS . D,
  e : 0.5 * (transpose(F) + F) - ident(3),
  volume * ( miu * mat_norm(e, frobenius)^2 + 0.5 * lam * mat_trace(e)^2 )
  ),  
  with_stdout(f, val_jac_hes_to_f90(linear_elastic_tet, [X, D, volume, lam, miu])),
  
  /* noehookean */
  neohookean_tet(X, D, volume, lam, miu) := block(
  [DS, F, I1, I3],
  DS : addcol(col(X, 2) - col(X, 1), 
	      col(X, 3) - col(X, 1),
	      col(X, 4) - col(X, 1)),
  F : DS . D,
  I1 : mat_trace(transpose(F) . F),
  I3 : determinant(transpose(F) . F),
  volume * ( 0.5  * miu *  ( I1 - log(I3) - 3 ) + 0.125 * lam * log(I3)^2 )
  ),
  with_stdout(f, val_jac_hes_to_f90(neohookean_tet, [X, D, volume, lam, miu])),
  
  /* fung hyperelastic material */
  fung_tet(X, D, volume, lam, miu, lam1, miu1, c) := block(
  0.5 * (stvk_tet(X, D, volume, lam, miu) + c * (exp(stvk_tet(X, D, volume, lam1, miu1)[1, 1]) - 1))
  ),
  with_stdout(f, val_jac_hes_to_f90(fung_tet, [X, D, volume, lam, miu, lam1, miu1, c])),
  
  return()
)$

close(f)$
  
  